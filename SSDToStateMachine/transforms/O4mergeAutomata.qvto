modeltype IOAutomatonModel uses "mm:ioautomaton";

//fourth transformation: merge the various automata created in the third transformation to one automaton per sender
transformation mergeAutomata(in automata: IOAutomatonModel, out mergedAutomaton: IOAutomatonModel);

main() {
	log("\n\n----------------------------------------------\nTransformation Automata -> Automaton");
	var automatonContainer := automata.objectsOfType(AutomatonContainer);
	automatonContainer->map mergeAutomata();	
}

// merge all automata together
mapping AutomatonContainer::mergeAutomata() : AutomatonContainer {
		
	self.automaton.sender->asSet()->forEach(sender) {
		log("Mapping automaton for " + sender);
		
		//select all automata with this sender
		var senderAutomata := self.automaton->select(automaton | automaton.sender = sender);
		//and merge them to one
		var resultAutomaton := senderAutomata->mergeAutomata();
		//set the sender of the merged automaton and add it to the container
		resultAutomaton.sender := sender;
		result.automaton += resultAutomaton;
	}
	
}

//merge a set of automata to one automaton
helper Set(Automaton)::mergeAutomata() : Automaton {
	var resultAutomaton := object Automaton {};
	
	//consolidate states and transitions by eliminating duplicates.
	var duplicateFreeStates := eliminateDuplicateStates(self);
	var duplicateFreeTransitions := eliminateDuplicateTransitions(self, duplicateFreeStates);
	
	//adjust the transtitions to the duplicate free states
	duplicateFreeStates->forEach(state) {
		adjustTransitions(state, duplicateFreeTransitions);
	};
	
	//assemble the merged automaton by using the duplicate free states and duplicate free transitions
	resultAutomaton.state := duplicateFreeStates;
	
	//set one of the merged automata's initial state as the initial state of the merged automaton (non-determinstic)
	resultAutomaton.initialstate := resultAutomaton.state-> findState(self->asSequence()->first().initialstate.name);	
	resultAutomaton.transition := duplicateFreeTransitions;
	return resultAutomaton;
}

//eliminate possible duplicate transitions
helper adjustTransitions(inout state: State, duplicateFreeTransitions: Set(Transition)) {
	
	log("Adjust transitions for state: " + state.name);
	
	//adjust incoming transitions
	var adjustedIncomingTransitions : Set(Transition);
	state.incomingtransition->forEach(transition) {
		adjustedIncomingTransitions += duplicateFreeTransitions->findTransition(transition);
	};
	state.incomingtransition := adjustedIncomingTransitions;
	
	//adjust outgoing transitions
	var adjustedOutgoingTransitions : Set(Transition);
	state.outgoingtransition->forEach(transition) {
		adjustedOutgoingTransitions += duplicateFreeTransitions->findTransition(transition);
	};
	state.outgoingtransition := adjustedOutgoingTransitions;	
}

//find a transition within a set of transitions
//The operation name, prestate, poststate and the outmessages must match exactly
query Set(Transition)::findTransition(t: Transition) : Transition {
	
	self->forEach(transition ) {
		if(	t.operation.name != transition.operation.name or 
			t.prestate.name != transition.prestate.name or
		 	t.poststate.name != transition.poststate.name or
		 	t.outmessage->size() != transition.outmessage->size()) {
		 	
		 	continue;
		};
		//check if both transitions have the same out messages
		var transitionsSetsNotEqual := false;
		t.outmessage->forEach(outMsg | not transition.outmessage->containsOutMessage(outMsg)){
			transitionsSetsNotEqual := true;
			break;
		};
		if(transitionsSetsNotEqual) {
			continue;
		};	
		return transition;
	};
	//default name of pre- and post state is null (needed for a nicer log if null occurrs)
	var postname := "null";
	if (t.poststate != null) {
		postname := t.poststate.name;
	};
	var prename := "null";
	if (t.prestate != null) {
		prename := t.prestate.name;
	};
	log("!!!!!fatal error: Transition in findTransition(mergeAutomaton.qvto) not found: " + prename + "->" + postname);
	return null;
}

//kick out all duplicate transitions
helper eliminateDuplicateTransitions(allAutomata: Set(Automaton), duplicateFreeStates:Set(State)) : Set(Transition) {
	var transitions : Set(Transition);
	
	allAutomata->forEach(automaton) {
		automaton.transition->forEach(transition | not transitions->containsTransition(transition)) {
			//create a transition using the duplicate free states if we haven't seen the transition yet
			transitions += createTransition(transition, duplicateFreeStates);
		}
	};
	return transitions;
}

//query to create a Transition between states
//states is a set
query createTransition(transition: Transition, states: Set(State)) : Transition {
	return object Transition {
		operation := createOperation(transition.operation.name);
		outmessage := createOutMessages(transition.outmessage);
		_return := createReturn(transition._return.value);
		prestate := states->findState(transition.prestate.name);
		poststate := states->findState(transition.poststate.name);
		log("created new transition: "+ prestate.name + " -> "+ poststate.name + "(" + operation.name + ")");
	}
	
	
}

//Find a state by name within a Set of states. Returns null if state is not found
query Set(State)::findState(stateName: String) : State {
	self->forEach(state | state.name = stateName) {
		return state;
	};
	log("!!!!!fatal error: state in findState(mergeAutomaton.qvto) not found: " + stateName);
	return null;
}

//Check if a transition is within a set of Transitions
query Set(Transition)::containsTransition(transition: Transition) : Boolean {
	self->forEach(t) {
		if(	t.operation.name != transition.operation.name or 
			t.prestate.name != transition.prestate.name or
		 	t.poststate.name != transition.poststate.name or
		 	t.outmessage->size() != transition.outmessage->size()) {
		 	
		 	continue;
		};
		
		//check if out messages of the current transition matches the argument's out messages
		var transitionsSetsNotEqual := false;
		t.outmessage->forEach(outMsg | not transition.outmessage->containsOutMessage(outMsg)){
			transitionsSetsNotEqual := true;
			break;
		};
		if(transitionsSetsNotEqual) {
			continue;
		};
		//check if the transitions have the same return values - if not, non-determinism is detected. This algorithm chooses the value of the first transition and logs the problem
		if (transition._return.value != t._return.value) {
			log("!!!!! NON-DETERMINISM FOUND IN TRANSITION " + t.prestate.name + " -> " + t.poststate.name + " - the return values " + t._return.value + " and " + transition._return.value + " were found. The Algorithm chooses " + t._return.value);
		};
		return true;	
	};
	return false;
}

//Check if a set of OutMessages contains one particular message
query Set(OutMessage)::containsOutMessage(outMessage: OutMessage) : Boolean {
	self->forEach(message) {
		
		//we only have a match if the return operation and object values are equal
		if(	message._return.value = outMessage._return.value and
			message.operation.name = outMessage.operation.name and 
			message._object.name = outMessage._object.name) {
			
			return true;
			
		}
	};
	return false;
}

//kick out all duplicate states that have the same name
helper eliminateDuplicateStates(allAutomata: Set(Automaton)) : Set(State) {
	var stateDict : Dict(String, State);
	
	allAutomata->forEach(automaton) {
		automaton.state->forEach(state | not stateDict->hasKey(state.name)) {
			var newState := object State {
				name := state.name;
			};
			stateDict->put(state.name, newState);
		}
	};
	
	return stateDict->values()->asSet();
}

//copy OutMessages
query createOutMessages(outMessage: OrderedSet(OutMessage)) : OrderedSet(OutMessage) {
	var resultMessages: OrderedSet(OutMessage);
	outMessage->forEach(message) {
		resultMessages += object OutMessage {
			operation := createOperation(message.operation.name);
			_object := createObject(message._object.name);
			_return := createReturn(message._return.value);
		}	
	};
	return resultMessages;
}

query createOperation(operationName: String): ioautomaton::Operation {
	return object ioautomaton::Operation {
		name := operationName
	}
}
query createReturn(returnValue: String): ioautomaton::Return {
	return object ioautomaton::Return {
		value := returnValue
	}
}
query createObject(objectName: String): ioautomaton::Object {
	return object ioautomaton::Object {
		name := objectName
	}
}




